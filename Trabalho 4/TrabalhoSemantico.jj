PARSER_BEGIN (TrabalhoSemantico)

import java.util.*;

public class TrabalhoSemantico{

static Hashtable<Integer, Dado> TabelaSimb = new Hashtable<Integer, Dado>();
static Hashtable<Integer, Dado> TabelaAux = new Hashtable<Integer, Dado>();
static Integer indexTabela = 0;
static Integer nivelCorrente = 0;
static String flagCategoria = "";
static Integer flagBranchStmt = 0;
static Integer nReturn = 0, procedureReturn = 0;
static Integer countArgs = 0;
static Dado dado = new Dado();
static ArrayList <Integer> listaChaves = new ArrayList <Integer> ();
static Token valor = null;

    public static void main(String[] args){
        try{
            TrabalhoSemantico t = new TrabalhoSemantico(new java.io.FileInputStream(args[0]));
            t.initBuiltins();
            t.Start();
//            System.out.println("-----------------------------------SIMB----------------------------------");
//            imprimeTabela(TabelaSimb);
//            System.out.println("-----------------------------------AUX-----------------------------------");
//            imprimeTabela(TabelaAux);
        } catch (java.io.FileNotFoundException ex) {
            System.out.println("Arquivo " + args[0] + " nao foi encontrado");
        } catch (ParseException ex) {
            System.out.println(ex.getMessage());
        } catch (TokenMgrError ex) {
            System.out.println(ex.getMessage());
        }
    }
}

PARSER_END (TrabalhoSemantico)

/* Eliminando comentarios e espacos */
SKIP: {" "|"\r"|"\n"|"\t"}
SKIP: {"/*" : BLOCKCOMMENT}
<BLOCKCOMMENT> SKIP:{"*/" : DEFAULT}
<BLOCKCOMMENT> MORE:{<~[]>}
SKIP: { <LINECOMMENT: "--" (~["\n"])* ("\n"|"\r"|"\r\n")>}

/* Palavras reservadas*/
TOKEN: { <ELSE: "else"> }
TOKEN: { <IF: "if"> }
TOKEN: { <ELSIF: "elsif"> }
TOKEN: { <WHILE: "while"> }
TOKEN: { <BREAK: "break"> }
TOKEN: { <RETURN: "return"> }
TOKEN: { <FOR: "for"> }
TOKEN: { <FOREACH: "foreach"> }
TOKEN: { <FUNCTION: "function"> }
TOKEN: { <TYPE: "type"> }
TOKEN: { <PROCEDURE: "procedure"> }
TOKEN: { <CONSTANT: "constant"> }
TOKEN: { <ENUM: "enum"> }
TOKEN: { <BY: "by"> }
TOKEN: { <SWITCH: "switch"> }
TOKEN: { <DO: "do">}
TOKEN: { <THEN: "then">}
TOKEN: { <CASE: "case"> }
TOKEN: { <CONTINUE: "continue"> }
TOKEN: { <RETRY: "retry"> }
TOKEN: { <EXIT: "exit"> }
TOKEN: { <TO: "to">}
TOKEN: { <PROFILE: "profile"> }
TOKEN: { <PROFILE_TIME: "profile_time"> }
TOKEN: { <TRACE: "trace"> }
TOKEN: { <BATCH: "batch"> }
TOKEN: { <TYPE_CHECK: "type_check"> }
TOKEN: { <INLINE: "inline"> }
TOKEN: { <WARNING: "warning"> }
TOKEN: { <SAVE: "save"> }
TOKEN: { <RESTORE: "restore"> }
TOKEN: { <STRICT: "strict"> }
TOKEN: { <LOOP: "loop"> }
TOKEN: { <GOTO: "goto"> }
TOKEN: { <NOT: "not"> }
TOKEN: { <AND: "and"> }
TOKEN: { <OR: "or"> }
TOKEN: { <XOR: "xor"> }
TOKEN: { <AS: "as"> }
TOKEN: { <LABEL: "label"> }
TOKEN: { <UNTIL: "until"> }
TOKEN: { <DOLAR: "$"> }
TOKEN: { <END: "end"> }
/* Identificadores */
TOKEN: { <#FUNCBUILT: "puts"|"print"|"lenght"|"printf"|"getc"|"find"
         |"sprintf"|"rand"|"sqrt"|"cos"|"sin" |"tan" |"log"|"power">}
TOKEN: { <#ALPHA: ["a"-"z"] | ["A"-"Z"]> }
TOKEN: { <#DIGITDEC: ["0"-"9"]> }
TOKEN: { <#DIGITHEX: ["0"-"9"] | ["A"-"F"]> }
TOKEN: { <#DIGITOCT: ["0"-"7"]> }
TOKEN: { <#DIGITBIN: ["0"-"1"]> }
TOKEN: { <#USCORE: "_"> }
TOKEN: { <SYMBOL: ["!"] | ["#"] | ["%"] | [":"-";"] | ["@"] | ["^"] | ["`"] | ["|"] | ["~"]> }
TOKEN: { <#CHAR: ( <SYMBOL> | <ALPHA> | <DIGITDEC>  ) > }
TOKEN: { <#ESCCHAR: ["\0"-"\b"] | ["\f"-"\r"]> }
TOKEN:
{
    <STRING: (("\""( "\\n" | <CHAR> | <ESCCHAR> | <USCORE> | "\\x"<DIGITHEX><DIGITHEX> | "\\u"<DIGITHEX><DIGITHEX><DIGITHEX><DIGITHEX>
              | "\\U"<DIGITHEX><DIGITHEX><DIGITHEX><DIGITHEX><DIGITHEX><DIGITHEX><DIGITHEX><DIGITHEX>)* "\"" | "\'"( "\\n" | <CHAR>
              | <ESCCHAR> |  <USCORE> | "\\x"<DIGITHEX><DIGITHEX> | "\\u"<DIGITHEX><DIGITHEX><DIGITHEX><DIGITHEX>
              | "\\U"<DIGITHEX><DIGITHEX><DIGITHEX><DIGITHEX><DIGITHEX><DIGITHEX><DIGITHEX><DIGITHEX>)* "\'" | "\"\"\"" ("\\n"
              | <CHAR> | <ESCCHAR> | <USCORE> | "\\x"<DIGITHEX><DIGITHEX> | "\\u"<DIGITHEX><DIGITHEX><DIGITHEX><DIGITHEX>
              | "\\U"<DIGITHEX><DIGITHEX><DIGITHEX><DIGITHEX><DIGITHEX><DIGITHEX><DIGITHEX><DIGITHEX>)* "\"\"\"")) | ("b\""(<DIGITBIN>
              | " " | <USCORE>)+ "\"") | ("x\""(<DIGITHEX> | " " | <USCORE>)+ "\"") >
}
TOKEN: { <IDENTIFIER: (<ALPHA>)(<ALPHA> | <DIGITDEC> | <USCORE>)* | (<USCORE>)(<ALPHA> | <DIGITDEC> | <USCORE>)* | "atom" | "integer" | "sequence" | "object" | <FUNCBUILT> > }
TOKEN: { <ATOMINTEGER: ("-")?(<DIGITDEC>)+ | ("-")?(<DIGITHEX>)+ | ("-")?(<DIGITOCT>)+| ("-")?(<DIGITBIN>)+ | ("-")?(<DIGITDEC>)"e"("+"|"-")?(<DIGITDEC>)+> }
TOKEN: { <ATOMREAL: (<DIGITDEC>)+"."(<DIGITDEC>)+> }     
TOKEN: { <ATOMBINARY: "0b"(<DIGITBIN>)+> }
TOKEN: { <ATOMOCTAL: "0t"(<DIGITBIN>)+> }
TOKEN: { <ATOMDECIMAL: "0d"(<DIGITBIN>)+> }
TOKEN: { <ATOMHEXADECIMAL: "0x"(<DIGITBIN>)+> }
TOKEN: { <BIN: <DIGITBIN> > }

/* Programa */
void Start() : {} { Stmtblk() <EOF> }
void Stmtblk() : {} { Statement() (Statement())* }
void Block() : {} { Statementblock() (Statementblock())* }
void Statement() : {}
{
    Breakstmt() | Return() | Continuestmt() | Retrystmt() | Exitstmt() | Forstmt() | Foreachstmt() | Whilestmt()
    | Loopstmt() | Gotostmt() | Ifstmt() | Switchstmt() | Look()
}
void Statementblock() : {}
{
    Breakstmt() | Return() | Continuestmt() | Retrystmt() | Exitstmt() | Forstmt() | Foreachstmt()
    | Whilestmt() | Loopstmt() | Gotostmt() | Ifstmt() | Switchstmt() | Lookblock()
}
void Look() : {}
{
    "?" Expression() | LOOKAHEAD(2) Assignwithop() | LOOKAHEAD(2) Call() | LOOKAHEAD(2) Assignmono() | LOOKAHEAD(2) Assignmulti() | Procdeclare()
    | Funcdeclare() | Typedeclare() | LOOKAHEAD(2) Vardeclare() | Constdeclare() | Enumdeclare()
}
void Lookblock() : {}
{
    "?" Expression() | LOOKAHEAD(2) Assignwithop() | LOOKAHEAD(2) Call() | LOOKAHEAD(2) Vardeclare() | Constdeclare()
    | Enumdeclare() | LOOKAHEAD(2) Assignmono() | Assignmulti()
}
void Breakstmt() : {} { <BREAK> (Stringlit() | Atomexpr())? }
void Continuestmt(): {} { <CONTINUE> (Stringlit()| Atomexpr())? }
void Retrystmt(): {} { <RETRY> (Stringlit()| Atomexpr())? }
void Exitstmt(): {} { <EXIT> (Stringlit()| Atomexpr())? }
void Stringlit() : {} { <STRING> }
void Forstmt() : {}
{
    <FOR> Foridx()
    (Labelstmt())? <DO>
    {
        flagBranchStmt++;
    }
    (Block())?
    <END> <FOR>
    {
        removeNivel(TabelaAux, flagBranchStmt);
        listaChaves.clear();
        flagBranchStmt--;
    }
}
void Foridx() : {} { <IDENTIFIER> "=" Expression() <TO> Expression() ( <BY> Expression())? }
void Foreachstmt() : {}
{
    <FOREACH>
    (<IDENTIFIER>)? "(" ( <IDENTIFIER> | Seqexpr() ) ")" (Labelstmt())? <DO>
    {
        flagBranchStmt++;
    }
    (Block())?
    <END> <FOREACH>
    {
        removeNivel(TabelaAux, flagBranchStmt);
        listaChaves.clear();
        flagBranchStmt--;
    }
}
void Labelstmt() : {} { <LABEL> Stringlit() }
void Whilestmt() : {}
{
    <WHILE>
    Expression() (Labelstmt())? <DO>
    {
        flagBranchStmt++;
    }
    (Block())?
    <END> <WHILE>
    {
        removeNivel(TabelaAux, flagBranchStmt);
        listaChaves.clear();
        flagBranchStmt--;
    }
}
void Loopstmt() : {}
{
    <LOOP> (Labelstmt())? <DO>
    {
        flagBranchStmt++;
    }
    Block() <UNTIL> Expression()
    <END> <LOOP>
    {
        removeNivel(TabelaAux, flagBranchStmt);
        listaChaves.clear();
        flagBranchStmt--;
    }
}
void Gotostmt() : {} { <GOTO> Labelstmt() }
void Ifstmt() : {}
{
    Iftest()
    (LOOKAHEAD(2) Elsif())* ( Else())?
    <END> <IF>
    {
        removeNivel(TabelaAux, flagBranchStmt);
        listaChaves.clear();
        flagBranchStmt--;
    }
}
void Iftest() : {}
{
    <IF> Expression() (Labelstmt())? <THEN>
    {
        flagBranchStmt++;
    }
    (Block())?
}
void Elsif() : {}
{
    <ELSIF>
    {
        removeNivel(TabelaAux, flagBranchStmt);
        listaChaves.clear();
    }
    Expression()<THEN> ( Block())?
}
void Else() : {}
{
    <ELSE>
    {
        removeNivel(TabelaAux, flagBranchStmt);
        listaChaves.clear();
    }
    (Block())?
}
void Switchstmt() : {}
{
    Switchtest()
    (Case())+ (Caseelse())?
    <END> <SWITCH>
    {
        removeNivel(TabelaAux, flagBranchStmt);
        listaChaves.clear();
        flagBranchStmt--;
    }
}
void Switchtest() : {}
{
    <SWITCH> Expression() (Labelstmt())? <DO>
    {
        flagBranchStmt++;
    }
}
void Case() : {} { <CASE>Caselist() <THEN> (Block())? }
void Caselist() : {} { Expression() ("," Expression())* }
void Caseelse() : {} { <ELSE> (Block())? }
void Assignmono() : {} { Varexp() "=" Expression()}
void Assignmulti() : {} { Varexp() ("," Varexp())* "=" Expression() ("," Expression())*}
void Assignwithop() : {} { Varexp() ( "+" | "-" | "/" | "*" | "&" ) "=" Expression() }
void Return() :
{
    if(flagBranchStmt == 0)
        nReturn++;

    procedureReturn++;
}
{
    <RETURN> Expression()
}
void Vardeclare() :
{
    Token t;
}
{
    t = <IDENTIFIER> Identlist()
    {
        atualizaVariavel(t, "var");
    }
}

void Identlist() : {}{ Ident() ("," Ident())* }

void Ident() :
{
    Token t;
}
{
    t = <IDENTIFIER> ("=" Expression())?
    {
        Dado tmp = new Dado();
        tmp.setIdentificador(t.toString());

        if(valor != null && flagCategoria.equals("constant")) {
            tmp.setValor(valor.toString());
            valor = null;
        }

        if(flagBranchStmt == 0) {
            tmp.setNivel(nivelCorrente);
            insereTabela(TabelaSimb, tmp);
        }
        else {
            if(naoDeclarado(TabelaSimb, t.toString(), nivelCorrente)) {
                tmp.setNivel(flagBranchStmt);
                insereTabela(TabelaAux, tmp);
            }
            else {
                System.out.println("Erro! Identificador \"" + tmp.getIdentificador() +  "\" já declarado");
            }
        }
    }
}

void Constdeclare() :
{
    Token t;
    flagCategoria = "constant";
}
{
    t = <CONSTANT> Identlist()
    {
        if(flagBranchStmt == 0)
            atualizaCategoria(TabelaSimb, t.toString());
        else
            atualizaCategoria(TabelaAux, t.toString());
        listaChaves.clear();
        flagCategoria = "";
    }
}

void Enumdeclare() : {}
{
    <ENUM> (Enumval() | Enumtype())
}
void Enumval() : {}
{
    (<BY> Enumdelta())? Identlist()
    {
        Integer chave = buscaTipo(TabelaSimb, "integer");
        if(flagBranchStmt == 0) {
            atualizaCategoria(TabelaSimb, "constant");
            atualizaTipo(TabelaSimb, chave);
        }
        else {
            atualizaCategoria(TabelaAux, "constant");
            atualizaTipo(TabelaAux, chave);
        }
        listaChaves.clear();
        flagCategoria = "";
    }
}
void Enumdelta() : {} { ("*" | "/")? Expression() }
void Enumtype() :
{
    Integer chave;
    Dado dado;
    Token t;
}
{
    <TYPE> (<BY> Enumdelta())?
    t = <IDENTIFIER>
    {
        // Neste caso precisa ser do tipo inteiro
        chave = buscaTipo(TabelaSimb, "integer");

        if(chave > -1) {
            dado = new Dado();
            dado.setIdentificador(t.toString());
            dado.setNivel(nivelCorrente);
            dado.setCategoria("type");
            dado.setTipo(chave);

            if(flagBranchStmt == 0)
                insereTabela(TabelaSimb, dado);
            else
                insereTabela(TabelaAux, dado);
        }
        else {
            System.out.println("Erro! Palavra reservada \"" + t.toString() + "\" não existe");
        }
        listaChaves.clear();
    }
    Identlist()
    {
        atualizaVariavel(t, "constant");
    }
    <END> <TYPE>
}
void Procdeclare() :
{
    Dado dado;
    Token t, s;
}
{
    t = <PROCEDURE> s = <IDENTIFIER>
    {
        dado = new Dado();
        dado.setIdentificador(s.toString());
        dado.setNivel(nivelCorrente);
        dado.setCategoria(t.toString());

        insereTabela(TabelaSimb, dado);
        listaChaves.clear();
        nivelCorrente++;
    }
    "(" (Paramlist())? ")" (Block())?
    {
        dado.setNParametros(listaChaves.size());
    }
    <END> <PROCEDURE>
    {
        if(procedureReturn > 0)
            System.out.println("Erro! Procedure não pode ter return!!");

        procedureReturn = 0;
        removeNivel(TabelaSimb, nivelCorrente);
        listaChaves.clear();
        nivelCorrente--;
    }
}
void Paramlist() : {}{ (Parameter() ("," Parameter())* ) }
void Parameter() :
{
    Dado dado;
    Token t, n;
}
{
    t = <IDENTIFIER> n = <IDENTIFIER>
    {
        Integer chave = buscaTipo(TabelaSimb, t.toString());

        if(chave > -1) {
            dado = new Dado();
            dado.setIdentificador(n.toString());
            dado.setNivel(nivelCorrente);
            dado.setTipo(chave);
            dado.setCategoria("parameter");

            insereTabela(TabelaSimb, dado);
        }
        else {
            System.out.println("Erro! Palavra reservada \"" + t.toString() + "\" não existe");
        }
    }
}
void Funcdeclare() :
{
    Dado dado;
    Token t, s;
}
{
    t = <FUNCTION>
    s = <IDENTIFIER>
    {
        dado = new Dado();
        dado.setIdentificador(s.toString());
        dado.setNivel(nivelCorrente);
        dado.setCategoria(t.toString());

        insereTabela(TabelaSimb, dado);
        listaChaves.clear();
        nivelCorrente++;
    }
    "("(Paramlist())?")"
    {
        dado.setNParametros(listaChaves.size());
    }
    (Block())?
    <END> <FUNCTION>
    {

        if(nReturn < 1)
            System.out.println("Erro! Function precisa de retorno!");
        nReturn = 0;
        removeNivel(TabelaSimb, nivelCorrente);
        listaChaves.clear();
        nivelCorrente--;
    }
}
void Typedeclare() :
{
    Token t, s;
}
{
    s = <TYPE>
    t = <IDENTIFIER>
    {
        dado = new Dado();
        dado.setIdentificador(t.toString());
        dado.setNivel(nivelCorrente);
        dado.setCategoria(s.toString());

        insereTabela(TabelaSimb, dado);
        listaChaves.clear();
        nivelCorrente++;

    }
    "(" Parameter() ")" (Block())?
    <END> <TYPE>
    {
        if(nReturn < 1)
            System.out.println("Erro! Type precisa de retorno!");
        nReturn = 0;

        removeNivel(TabelaSimb, nivelCorrente);
        listaChaves.clear();
        nivelCorrente--;
    }
}
void Expression() : {} { Binexpr() }
void Binexpr() : {} { Relexpr() (Binop() Relexpr())* }
void Binop() : {} { <AND> | <OR> | <XOR> }
void Relexpr() : {} { Concatexpr() (Relop() Concatexpr())* }
void Relop() : {} { "<" | ">" | "<=" | ">=" |"="| "!=" }
void Concatexpr() : {} { Sumsubexpr() (Concat() Sumsubexpr())* }
void Concat() : {} { "&" }
void Sumsubexpr() : {} { Multidivexpr() (Sumsub() Multidivexpr())*  }
void Sumsub() : {} { "+" | "-" }
void Multidivexpr() : {} { Unaryexpr() (Multidiv() Unaryexpr())* }
void Multidiv() : {} { "*" | "/" }
void Unaryexpr(): {} { (Unaryop())? Lastexpr() }
void Unaryop() : {} { <NOT> | "-" | "+" }
void Lastexpr() :
{
    Token t;
}
{
    Atomexpr()
    |
    Stringlit()
    |
    <BIN>
    |
    Seqexpr()
    |
    "("Expression()")"
    |
    t = <IDENTIFIER>
    (Variable() | Callf(t)
    {
        if(naoDeclarado(TabelaSimb, t.toString(), 0)) {
            System.out.println("Erro! A função \"" + t.toString() + "\" não foi declarada!\n");
        }

        String Categoria = buscaCategoria(TabelaSimb, t.toString(), 0);

        if(Categoria != null && !Categoria.equals("function")) {
            System.out.println("Erro! O procedure \"" + t.toString() + "\" não retorna nenhum valor.\n");
        }
    })?
    {
        naoDeclaradoFluxo(t);
    }
}
void Variable() : {} { ( LOOKAHEAD(3) Slice() | ("["Expression()"]")+ ) }
void Varexp() :
{
    Token t;
}
{
    t = <IDENTIFIER> ( LOOKAHEAD(3) Slice() | ("["Expression()"]")+ )?
    {
        naoDeclaradoFluxo(t);
    }
}
void Slice() : {} { "[" Expression() ".." (Expression() | <DOLAR>) "]"}
void Seqexpr() : {} { "{" (Expression() (("," Expression())+ | <DOLAR>)?)? "}" }
void Call() :
{
    Token t;
}
{
    t = <IDENTIFIER>
    {
        if(naoDeclarado(TabelaSimb, t.toString(), 0)) {
            System.out.println("Erro! A função \"" + t.toString() + "\" não foi declarada!\n");
        }
    }
    "(" (Arglist())? ")"
    {
        Integer nParam = getNParametros(TabelaSimb, t.toString(), 0);

        if(nParam != countArgs)
        {
            System.out.println("Erro! " + t.toString() + " esperava " + nParam + " argumentos mas recebeu " + countArgs + " .");
        }
        countArgs = 0;
    }
}
void Callf(Token t) : {}
{
    "(" (Arglist())? ")"
    {
        Integer nParam = getNParametros(TabelaSimb, t.toString(), 0);

        if(nParam != countArgs)
        {
            System.out.println("Erro! " + t.toString() + " esperava " + nParam + " argumentos mas recebeu " + countArgs + " .");
        }
        countArgs = 0;
    }
}
void Arglist() : {}
{
    Expression() { countArgs++; }
    ("," Expression() { countArgs++; })*
}
void Objexpr() : {} { Atomexpr() | Seqexpr() | <STRING> }
void Atomexpr() : {}{ <ATOMINTEGER> | <ATOMREAL> | <ATOMBINARY> | <ATOMOCTAL> | <ATOMDECIMAL> | <ATOMHEXADECIMAL> }


/* Funcoes Auxiliares */
boolean naoDeclarado(Hashtable<Integer, Dado> tabela, String Identificador, Integer NivelProcurado) :
{
    Enumeration t = tabela.keys();
    Integer chave;
    Dado dado;
}
{
    {
        while (t.hasMoreElements()) {
            chave = (Integer)t.nextElement();
            dado = tabela.get(chave);

            if(dado.getIdentificador().equals(Identificador) && dado.getNivel() == NivelProcurado){
                return false;/* ja existe na tabela*/
            }
        }
        /* pode ser adicionado futuramente na tabela */
        return true;
    }
}

void naoDeclaradoFluxo(Token t) : {}
{
    {
        if(naoDeclarado(TabelaSimb, t.toString(), nivelCorrente)) {
            int i = 0;
            for(i = flagBranchStmt; naoDeclarado(TabelaAux, t.toString(), i) && i > 0; i--);

            if(i == 0)
                System.out.println("Erro! Variável \"" + t.toString() + "\" não declarada no nivel atual!");
        }
    }
}

void insereTabela(Hashtable<Integer, Dado> tabela, Dado dado) : {}
{
    {
        if(naoDeclarado(tabela, dado.getIdentificador(), dado.getNivel())){
            tabela.put(indexTabela, dado);
            listaChaves.add(indexTabela);
            indexTabela++;
        }
        else {
            System.out.println("Erro! Identificador \"" + dado.getIdentificador() +  "\" já declarado no nível atual");
        }
    }
}

Integer buscaTipo(Hashtable<Integer, Dado> tabela, String Identificador) :
{
    Integer chave;
    Enumeration elemento = tabela.keys();
    Dado dado;
}
{
    {
        while(elemento.hasMoreElements()) {
            chave = (Integer) elemento.nextElement();
            dado = tabela.get(chave);

            if(dado.getIdentificador().equals(Identificador) && dado.getCategoria().equals("type"))
                return chave;
        }
        return -1;
    }
}

String buscaCategoria(Hashtable<Integer, Dado> tabela, String Identificador, Integer Nivel) :
{
    Integer chave;
    Enumeration elemento = tabela.keys();
    Dado dado;
}
{
    {
        while(elemento.hasMoreElements()) {
            chave = (Integer) elemento.nextElement();
            dado = tabela.get(chave);

            if(dado.getIdentificador().equals(Identificador) && dado.getNivel() == Nivel)
                return dado.getCategoria();
        }
        return null;
    }
}

Integer getNParametros(Hashtable<Integer, Dado> tabela, String Identificador, Integer Nivel) :
{
    Integer chave;
    Enumeration elemento = tabela.keys();
    Dado dado;
}
{
    {
        while(elemento.hasMoreElements()) {
            chave = (Integer) elemento.nextElement();
            dado = tabela.get(chave);

            if(dado.getIdentificador().equals(Identificador) && Nivel == dado.getNivel())
                return dado.getNParametros();
        }

        return -1;
    }
}

void imprimeTabela(Hashtable<Integer, Dado> tabela) :
{
    Integer chave;
    Enumeration elemento = tabela.keys();
    Dado dado;
}
{
    {

        System.out.printf("%-4s%-10s%-6s%-10s%-6s%-6s%-6s%-6s%-10s%-10s\n", "ID", "IDENT", "NIVEL", "CATEGORIA", "TIPO", "END", "NBYTE", "VALOR", "CLASST", "NPARAM");
        System.out.println("-------------------------------------------------------------------------");
        while(elemento.hasMoreElements()) {
            chave = (Integer) elemento.nextElement();
            dado = tabela.get(chave);

            System.out.printf("%-4s%-10s%-6s%-10s%-6s%-6s%-6s%-6s%-10s%-10s\n",
                              chave, dado.getIdentificador(), dado.getNivel(),
                              dado.getCategoria(), dado.getTipo(), dado.getEndereco(),
                              dado.getNBytes(), dado.getValor(), dado.getClasseTransf(),
                              dado.getNParametros());
        }

        System.out.println("");
    }
}

void atualizaCategoria(Hashtable<Integer, Dado> tabela,String Categoria) :
{
    Dado dado;
}
{
    {
        for(Integer chave : listaChaves)
        {
            dado = tabela.get(chave);
            dado.setCategoria(Categoria);
        }

    }
}

void atualizaTipo(Hashtable<Integer, Dado> tabela, Integer Tipo) :
{
    Dado dado;
}
{
    {
        for(Integer chave : listaChaves)
        {
            dado = tabela.get(chave);
            dado.setTipo(Tipo);
        }
    }
}

void atualizaNParam(Hashtable<Integer, Dado> tabela, Integer n) :
{
    Dado dado;
}
{
    {
        for(Integer chave : listaChaves)
        {
            dado = tabela.get(chave);
            dado.setNParametros(n);
        }
    }
}
void atualizaNBytes(Hashtable<Integer, Dado> tabela, Integer nbytes) :
{
    Dado dado;
}
{
    {
        for(Integer chave : listaChaves)
        {
            dado = tabela.get(chave);
            dado.setNBytes(nbytes);
        }
    }
}

void removeNivel(Hashtable<Integer, Dado> tabela, Integer nivel) :
{
    Integer chave;
    Enumeration t = tabela.keys();
    Dado dado;
}
{
    {
        while(t.hasMoreElements())
        {
            chave = (Integer) t.nextElement();
            dado = tabela.get(chave);

            if(dado.getNivel() == nivel) {
                tabela.remove(chave);
            }
        }
    }
}

void removeChaves(Hashtable<Integer, Dado> tabela, ArrayList<Integer> chaves) : {}
{
    {
        for(Integer chave : chaves) {
            tabela.remove(chave);
        }
    }
}

void atualizaVariavel(Token t, String Categoria) : {}
{
    {
        Hashtable <Integer, Dado> tabela = TabelaSimb;
        Integer chave = buscaTipo(TabelaSimb, t.toString());

        if(chave == -1) {
            tabela = TabelaAux;
            chave = buscaTipo(tabela, t.toString());
        }

        if(chave > -1) {
            Dado builtin = tabela.get(chave);
            if(flagBranchStmt == 0) {
                atualizaTipo(TabelaSimb, chave);
                atualizaNBytes(TabelaSimb, builtin.getNBytes());
                atualizaCategoria(TabelaSimb, Categoria);
            }
            else {
                atualizaTipo(TabelaAux, chave);
                atualizaNBytes(TabelaAux, builtin.getNBytes());
                atualizaCategoria(TabelaAux, Categoria);
            }
        }
        else {
            System.out.println("Erro! O tipo \"" + t.toString() + "\" não existe!\n");

            if(flagBranchStmt == 0)
                removeChaves(TabelaSimb, listaChaves);
            else
                removeChaves(TabelaAux, listaChaves);
        }
        listaChaves.clear();
    }
}

void initBuiltins(): {}
{
    {
        //Atom
        Dado atom = new Dado();
        atom.setIdentificador("atom");
        atom.setCategoria("type");
        atom.setTipo(indexTabela);
        // FIXME: Definir o tamanho em bytes
        atom.setNBytes(5);
        atom.setNivel(0);

        insereTabela(TabelaSimb, atom);

        //Integer
        Dado integer = new Dado();
        integer.setIdentificador("integer");
        integer.setCategoria("type");
        integer.setTipo(indexTabela);
        // FIXME: Definir o tamanho em bytes
        integer.setNBytes(4);
        integer.setNivel(0);

        insereTabela(TabelaSimb, integer);

        Dado sequence = new Dado();
        sequence.setIdentificador("sequence");
        sequence.setCategoria("type");
        sequence.setTipo(indexTabela);
        sequence.setNivel(0);

        insereTabela(TabelaSimb, sequence);

        Dado object = new Dado();
        object.setIdentificador("object");
        object.setCategoria("type");
        object.setTipo(indexTabela);
        object.setNivel(0);

        insereTabela(TabelaSimb, object);
        //?
        Dado question = new Dado();
        question.setIdentificador("?");
        question.setCategoria("procedure");
        question.setTipo(indexTabela);
        question.setNivel(0);

        insereTabela(TabelaSimb, question);

        //print
        Dado print = new Dado();
        print.setIdentificador("print");
        print.setCategoria("procedure");
        print.setNParametros(3);
        print.setTipo(indexTabela);
        print.setNivel(0);

        insereTabela(TabelaSimb, print);

        //puts
        Dado puts = new Dado();
        puts.setIdentificador("puts");
        puts.setCategoria("procedure");
        puts.setNParametros(2);
        puts.setTipo(indexTabela);
        puts.setNivel(0);

        insereTabela(TabelaSimb, puts);

        Dado printf = new Dado();
        printf.setIdentificador("printf");
        printf.setCategoria("procedure");
        printf.setNParametros(3);
        printf.setTipo(indexTabela);
        printf.setNivel(0);

        insereTabela(TabelaSimb, printf);
        Dado length = new Dado();
        length.setIdentificador("length");
        length.setCategoria("function");
        length.setNParametros(1);
        length.setTipo(indexTabela);
        length.setNivel(0);

        insereTabela(TabelaSimb, length);

        Dado getc = new Dado();
        getc.setIdentificador("getc");
        getc.setCategoria("function");
        getc.setNParametros(1);
        getc.setTipo(indexTabela);
        getc.setNivel(0);

        insereTabela(TabelaSimb, getc);

        Dado find = new Dado();
        find.setIdentificador("find");
        find.setCategoria("function");
        find.setNParametros(3);
        find.setTipo(indexTabela);
        find.setNivel(0);

        insereTabela(TabelaSimb, find);

        Dado sprintf = new Dado();
        sprintf.setIdentificador("sprintf");
        sprintf.setCategoria("function");
        sprintf.setNParametros(2);
        sprintf.setTipo(indexTabela);
        sprintf.setNivel(0);

        insereTabela(TabelaSimb, sprintf);

        Dado rand = new Dado();
        rand.setIdentificador("rand");
        rand.setCategoria("function");
        rand.setNParametros(1);
        rand.setTipo(indexTabela);
        rand.setNivel(0);

        insereTabela(TabelaSimb, rand);

        Dado sqrt = new Dado();
        sqrt.setIdentificador("sqrt");
        sqrt.setCategoria("function");
        sqrt.setNParametros(1);
        sqrt.setTipo(indexTabela);
        sqrt.setNivel(0);

        insereTabela(TabelaSimb, sqrt);

        Dado tan = new Dado();
        tan.setIdentificador("tan");
        tan.setCategoria("function");
        tan.setNParametros(1);
        tan.setTipo(indexTabela);
        tan.setNivel(0);

        insereTabela(TabelaSimb, tan);

        Dado sin = new Dado();
        sin.setIdentificador("sin");
        sin.setCategoria("function");
        sin.setNParametros(1);
        sin.setTipo(indexTabela);
        sin.setNivel(0);

        insereTabela(TabelaSimb, sin);

        Dado cos = new Dado();
        cos.setIdentificador("cos");
        cos.setCategoria("function");
        cos.setNParametros(1);
        cos.setTipo(indexTabela);
        cos.setNivel(0);

        insereTabela(TabelaSimb, cos);

        Dado log = new Dado();
        log.setIdentificador("log");
        log.setCategoria("function");
        log.setNParametros(1);
        log.setTipo(indexTabela);
        log.setNivel(0);

        insereTabela(TabelaSimb, log);

        Dado power = new Dado();
        power.setIdentificador("power");
        power.setCategoria("function");
        power.setNParametros(2);
        power.setTipo(indexTabela);
        power.setNivel(0);

        insereTabela(TabelaSimb, power);

        listaChaves.clear();
    }
}
